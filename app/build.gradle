apply plugin: 'com.android.application'

apply from: "$rootDir/tools/versions.gradle"
apply from: "$rootDir/tools/keys.gradle"

android {
    compileSdkVersion config.compileSdkVersion

    defaultConfig {
        applicationId "com.diegomalone.movielist"
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def STRING = "String"
        def API_URL = "API_URL"
        def API_KEY = "API_KEY"
        def IMAGE_URL = "IMAGE_URL"

        release {
            buildConfigField STRING, API_URL, "\"http://api.themoviedb.org\""
            buildConfigField STRING, API_KEY, tmdb_api_key
            buildConfigField STRING, IMAGE_URL, "\"https://image.tmdb.org/t/p/w500\""

            minifyEnabled false
        }

        debug {
            buildConfigField STRING, API_URL, "\"http://api.themoviedb.org\""
            buildConfigField STRING, API_KEY, tmdb_api_key
            buildConfigField STRING, IMAGE_URL, "\"https://image.tmdb.org/t/p/w500\""

            debuggable true
        }
    }

    sourceSets {
        main {
            file('src/main/res-screen')
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    deps.test.each {
        add(it.configuration, it.dependency)
    }

    deps.implementation.each { _group, implementation ->
        implementation.each { _name, dependency ->
            add("implementation", dependency)
        }
    }
}